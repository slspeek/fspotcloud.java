apply from: 'libraries.gradle'

buildscript {
    repositories {
        mavenCentral()
        add(new org.apache.ivy.plugins.resolver.URLResolver()) {
            name = 'GitHub'
            addArtifactPattern 'http://cloud.github.com/downloads/[organisation]/[module]/[module]-[revision].[ext]'
        }
        maven {
            url "http://fspotcloud-maven.googlecode.com/svn/maven/repository"

        }
    }

    dependencies {
        classpath 'bmuschko:gradle-gae-plugin:0.5.4'
        classpath 'bmuschko:gradle-tomcat-plugin:0.9.1'
        classpath 'desource:gradle-gwt-plugin:0.3'
    }
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    defaultTasks 'build'
    group = 'com.googlecode.fspotcloud'
    version = '0.12-04g'
}

subprojects {
    configurations {
        jalopy
    }
    apply plugin: 'java'
    apply plugin: 'jdepend'
    apply plugin: 'findbugs'
    apply plugin: 'checkstyle'


    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    //ext.libs = libs
    checkstyle {
        configFile = file("../config/checkstyle/checkstyle.xml")
    }

    [checkstyleMain,checkstyleTest]*.ignoreFailures = true
    findbugs {
      ignoreFailures = true
    }

    findbugsTest.enabled = false

    dependencies {
        jalopy  'jalopy:jalopy-ant:0.1-1.5b5', 'jalopy:jalopy:1.5rc3'
    }

    task jalopy(dependsOn: classes) << {

        ant.taskdef(name: 'jalopy',
                classname: 'de.hunsicker.jalopy.plugin.ant.AntPlugin',
                classpath: configurations.jalopy.asPath)
        ant.jalopy(convention: "${rootDir}/config/jalopy/jalopy-convention.xml") {
            fileset(dir: "src/main/java") {
                include(name: '**/*.java')
            }
        }
        ant.jalopy(convention: "${rootDir}/config/jalopy/jalopy-convention.xml") {
            fileset(dir: "src/test/java") {
                include(name: '**/*.java')
            }
        }
    }

    repositories {
        mavenCentral()
        maven {
            url "http://fspotcloud-maven.googlecode.com/svn/maven/repository"
        }
        maven { url "http://maven-gae-plugin.googlecode.com/svn/repository"}

        maven {
            url "http://gwt-maven.googlecode.com/svn/trunk/mavenrepo/"
        }
        maven {
            url "http://www.datanucleus.org/downloads/maven2"
        }
        mavenLocal()
    }

    check.dependsOn jalopy
}


//dependsOnChildren()
