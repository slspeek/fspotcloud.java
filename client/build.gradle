description = 'GWT client'

apply plugin: 'gwt2'

[findbugsMain, findbugsTest]*.enabled = false

configurations {
    javascript
}

dependencies {
    compile project(':rpc')
    compile project(path: ':rpc', configuration: 'sources')
    compile "com.google.inject.extensions:guice-assistedinject:${guice_version}"
    compile "com.google.gwt.inject:gin:1.5.0"
    compile libs.guice
    compile libs.inject
    compile libs.guava
    compile libs.guava_gwt
    compile "com.google.gwt:gwt-user:${gwt_version}"
    compile "com.reveregroup.gwt:gwt-image-loader:1.1.4"
    compile "com.google.gwt:gwt-dev:${gwt_version}"

    testCompile "junit:junit:3.8.1"
    testCompile libs.junit
    testCompile libs.jukito
    testCompile libs.mockito
    testCompile "org.jmock:jmock:2.5.1"
    testCompile libs.gwt_test_utils
}

task convertIcons(type: Exec, dependsOn: classes) {
    inputs.dir "src/main/resources/images/originals"
    outputs.dir "build/classes/main/images/"
    commandLine "./convert_gradle.sh", "${icon_size}"
}

task writeVersion(type: Exec, dependsOn: classes) {
    outputs.file "build/classes/main/version.txt"
    commandLine "./write_version_gradle.sh", "${version}"
}

task javascriptJar(type: Jar, dependsOn: compileGwt) {
    classifier = 'javascript'
    excludes = ['**/WEB-INF/web.xml']
    from "${buildDir}/gwt/out",
            "${rootDir}/client/src/main/webapp",
            "${rootDir}/client/src/main/resources"
}

compileGwt.dependsOn convertIcons, writeVersion
gwtDevMode.dependsOn convertIcons, writeVersion
test {
    doFirst()
    {
        exclude '**/*GwtTest*'
        //testLogging.showStandardStreams = true
        jvmArgs '-Xmx256m', '-XX:+HeapDumpOnOutOfMemoryError'
    }
}

gwtModules = ['com.googlecode.fspotcloud.FSpotCloud', 'com.googlecode.fspotcloud.dashboard']

compileGwt {
    style = 'DETAILED' // Script output style: OBF[USCATED], PRETTY, or DETAILED (defaults to OBF)
    compileReport = true
}

gwtDevMode {
    startupUrls = ["http://localhost:8050/j2ee-e2e/FSpotCloud.html", "http://localhost:8080/Dashboard.html",
            "http://localhost:8080/FSpotCloud.html"]
}

artifacts {
    javascript javascriptJar
}
